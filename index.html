<!DOCTYPE html>
<html>
    
    <!-- Head -->
    <head>
        <title>World Generation</title>
        <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">   
        
        <!-- Simply so i don't get the error that i have no favicon-->
        <link rel="shortcut icon" href="">

        <!-- Styling -->
        <link rel="stylesheet" type="text/css" href="style.css">

        <!-- Libraries -->
        <script src="./lib/dat.gui.min.js"></script>

        <script src="./lib/three.js"></script>
        <script src="./lib/three-stats.js"></script>
        <script src="./lib/three-detector.js"></script>       
        <script src="./lib/three-orbit.js"></script>    

    </head>
    
    <!-- Body -->
    <body>

         <!-- Fractal Terrain Generator -->
         <script src ="fractal-terain.js"></script>

         <!-- World Builder -->
         <script src="world-builder.js"></script>

        <!-- Terrain Vertex Shader -->
        <script id="vertexShader" type="x-shader/x-vertex">
            varying vec2 vUv;
            varying vec3 vPosition;

            varying vec3 vNormal;
            varying vec3 vEyeVec;

            void main(){

                vUv = uv;
                vPosition = position;
                vNormal = normal;
                vEyeVec = cameraPosition;   

                gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);   
            }
        </script>

        <!-- Terrain Fragment Shader -->
        <script id="fragmentShader" type="x-shader/x-fragment">
           // Textures
            uniform sampler2D texture_sand;
            uniform sampler2D texture_grass;
            uniform sampler2D texture_mountain;
            uniform sampler2D texture_snow;
            
            uniform float max_sand_level;
            uniform float max_grass_level;
            uniform float max_mountain_level;
            uniform float max_snow_level;

            uniform float blend_offset;

            //Light and shadow 
            uniform float shininess;
            uniform vec3 light_direction;
            
            uniform vec4 light_diffuse;
            uniform vec4 light_ambient;
            uniform vec4 light_specular;
            
            uniform vec4 material_ambient;
            uniform vec4 material_specular;

            varying vec2 vUv;
            varying vec3 vPosition;

            varying vec3 vNormal;
            varying vec3 vEyeVec;

            void main(){

                // Texture 
                vec4 color = texture2D(texture_sand, vUv);

                color = mix(color, texture2D(texture_grass, vUv), smoothstep(max_sand_level - blend_offset, max_sand_level + blend_offset, vPosition.z));
                color = mix(color, texture2D(texture_mountain, vUv), smoothstep(max_grass_level - blend_offset, max_grass_level + blend_offset, vPosition.z));
                color = mix(color, texture2D(texture_snow, vUv), smoothstep(max_mountain_level - blend_offset, max_mountain_level + blend_offset, vPosition.z));


                // Light and shadow
                vec3 L = normalize(light_direction);
                vec3 N = normalize(vNormal);

                float lambert_term = dot(N, -L);

                vec4 Ia = light_ambient * material_ambient;            // Ambient
                vec4 Id = vec4(0.0, 0.0, 0.0, 1.0);                    // Diffuse
                vec4 Is = vec4(0.0, 0.0, 0.0, 1.0);                    // Specular
                
                if(lambert_term > 0.0) {

                    Id = light_diffuse * color * lambert_term;

                    vec3 E = normalize(vEyeVec);
                    vec3 R = reflect(L, N);
                    float specular = pow(max(dot(R, E), 0.0), shininess);

                    Is = light_specular * material_specular * specular;
                }

                gl_FragColor = Ia + Id + Is;
            }

        </script>

        <!-- Water Vertex Shader -->
        <script id="waterVertexShader" type="x-shader/x-vertex">
            
            // Perlin noise implementation as seen at
            // https://github.com/Jojocaster/Ocean-Shaders

            vec3 mod289(vec3 x)
            {
                return x - floor(x * (1.0 / 289.0)) * 289.0;
            }
           
            vec4 mod289(vec4 x)
            {
                return x - floor(x * (1.0 / 289.0)) * 289.0;
            }
            
            vec4 permute(vec4 x)
            {
                return mod289(((x*34.0)+1.0)*x);
            }
            
            vec4 taylorInvSqrt(vec4 r)
            {
                return 1.79284291400159 - 0.85373472095314 * r;
            }
            
            vec3 fade(vec3 t) {
                return t*t*t*(t*(t*6.0-15.0)+10.0);
            }
            
            float pnoise(vec3 P, vec3 rep, float heightPerturb)
            {
                vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
                vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
                Pi0 = mod289(Pi0);
                Pi1 = mod289(Pi1);
                vec3 Pf0 = fract(P); // Fractional part for interpolation
                vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
                vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
                vec4 iy = vec4(Pi0.yy, Pi1.yy);
                vec4 iz0 = Pi0.zzzz;
                vec4 iz1 = Pi1.zzzz;
                vec4 ixy = permute(permute(ix) + iy);
                vec4 ixy0 = permute(ixy + iz0);
                vec4 ixy1 = permute(ixy + iz1);
                vec4 gx0 = ixy0 * (1.0 / 7.0);
                vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
                gx0 = fract(gx0);
                vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
                vec4 sz0 = step(gz0, vec4(0.0));
                gx0 -= sz0 * (step(0.0, gx0) - 0.5);
                gy0 -= sz0 * (step(0.0, gy0) - 0.5);
                vec4 gx1 = ixy1 * (1.0 / 7.0);
                vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
                gx1 = fract(gx1);
                vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
                vec4 sz1 = step(gz1, vec4(0.0));
                gx1 -= sz1 * (step(0.0, gx1) - 0.5);
                gy1 -= sz1 * (step(0.0, gy1) - 0.5);
                vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
                vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
                vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
                vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
                vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
                vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
                vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
                vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
                vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
                g000 *= norm0.x;
                g010 *= norm0.y;
                g100 *= norm0.z;
                g110 *= norm0.w;
                vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
                g001 *= norm1.x;
                g011 *= norm1.y;
                g101 *= norm1.z;
                g111 *= norm1.w;
                float n000 = dot(g000, Pf0);
                float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
                float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
                float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
                float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
                float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
                float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
                float n111 = dot(g111, Pf1);
                vec3 fade_xyz = fade(Pf0);
                vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
                vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
                float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
                return heightPerturb * n_xyz;
            }

            uniform float time;
            varying vec2 vUv;

            void main() 
            {
                vUv = uv;

                // Apply displacement based on noise, time and vertex normal 
                float displacement =  5.0 * pnoise( 0.05 * position + vec3(2.0 * time), vec3( 100.0 ), 4.5);
                vec3 newPosition = position + normal * displacement;
                
                gl_Position = projectionMatrix * modelViewMatrix * vec4(newPosition, 1.0);
            }
        
        </script>

        <!-- Water Fragment Shader -->
        <script id="waterFragmentShader" type="x-shader/x-fragment">
            uniform sampler2D texture_water;
            uniform float alpha;

            varying vec2 vUv;

            void main() 
            {
                vec4 color = texture2D(texture_water, vUv);
                color.a = alpha;
                gl_FragColor = color;
            }
        
        </script>


    </body>
</html>